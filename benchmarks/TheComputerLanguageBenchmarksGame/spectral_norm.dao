#{
-- The Computer Language Shootout
-- http://shootout.alioth.debian.org/
-- contributed by Isaac Gouy, tuned by Mike Pall
-- modified by Fu Limin for Dao language.
#}

routine Av( n :int, x :array<double>, y :array<double>)
{
	for( i = 0 : n-1 ){
		a = 0D;
		for( j = 0 : n-1){
			ij = i + j;
			a += x[j] / ( (ij*(ij+1)) / 2 + i+1);
		}
		y[i] = a;
	}
}

routine Atv(n :int, x :array<double>, y :array<double>)
{
	for( i = 0 : n-1 ){
		a = 0D;
		for( j = 0 : n-1){
			ij = i + j;
			a += x[j] / ( (ij*(ij+1)) / 2 + j+1);
		}
		y[i] = a;
	}
}

routine AtAv(n :int, x :array<double>, y :array<double>, t :array<double>)
{
	Av(n, x, t);
	Atv(n, t, y);
}

routine main( n = 100 )
{
	u = [ 1D : n ];
	v = [ 1D : n ];
	t = [ 1D : n ];

	for( i = 1 : 10 ){
		AtAv(n, u, v, t);
		AtAv(n, v, u, t);
	}

	vBv, vv = 0D;
	for( i = 0 : n-1 ){
		ui = u[i];
		vi = v[i];
		vBv = vBv + ui*vi;
		vv = vv + vi*vi;
	}
	io.writef("%0.9f\n", sqrt(vBv / vv));
}
