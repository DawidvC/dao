
interface MethodInterface
{
	routine Method( a = 0 );
}

routine TestMethodInterface( inter : MethodInterface )
{
	inter.Method( 123 )
}

class MethodClass
{
	routine Method( a = 0 ){ io.writeln( a ) }
}


@[test()]
TestMethodInterface( MethodClass() )
@[test()]
@[test()]
123
@[test()]



interface OperatorInterface
{
	operator []( index : int ) => int;
	operator .name() => string;
}

routine TestOperatorInterface( inter : OperatorInterface )
{
	io.writeln( inter[123] );
	io.writeln( inter.name );
}

class OperatorClass
{
	operator []( index : int ){ return index }
	operator .name(){ return "name" }
}


@[test()]
TestOperatorInterface( OperatorClass() )
@[test()]
@[test()]
123
name
@[test()]





interface IteratorInterface
{
	operator for( iter : for_iterator );
	operator []( iter : for_iterator )=>int;
}

routine TestIteratorInterface( inter : IteratorInterface )
{
	for( i in inter ) io.writeln( i )
}

class IteratorClass
{
	operator for( iter : for_iterator ){
		iter.valid = 1;
		iter.iterator = 0;
	}
	operator []( iter : for_iterator ){
		id = (int)iter.iterator;
		iter.valid = id + 1 < 5;
		iter.iterator = id + 1;
		return id;
	}
}


@[test()]
TestIteratorInterface( IteratorClass() )
@[test()]
@[test()]
0
1
2
3
4
@[test()]





interface JointInterface : MethodInterface, OperatorInterface, IteratorInterface
{
}

routine TestJointInterface( inter : JointInterface )
{
	TestMethodInterface( inter )
	TestOperatorInterface( inter )
	TestIteratorInterface( inter )
}

class JointClass ( MethodClass, OperatorClass, IteratorClass )
{
}


@[test()]
TestJointInterface( JointClass() )
@[test()]
@[test()]
123
123
name
0
1
2
3
4
@[test()]
