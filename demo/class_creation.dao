
class Base
{
	var name = 'Base';
}

routine CreateClass( name : string, value = 0 )
{
	io.writeln( name, value );

	rout = routine( self : @class ){ # @class for host class type
		io.writeln( 'Extra()', self.index );
	}
	# to be used as initializer/constructor
	init = routine( self : @class, txt : string ){
		io.writeln( 'initialized by init' );
		self.text = txt;
	}

	# klass = class( name, parents, fields, methods ){ core_class_body }
	klass = class( name, 
			{ 'Alias'=>Base }, 
			{ ('added', 789, $const, $public) }, 
			{ (name, init), ('Extra', rout) } )
	{
		var index = value;
		var text = '---';

		routine @class( id = 0 ){ index = id }

		routine Meth( id = 0 ){ io.writeln( 'Meth(id)', id, index, text ) }
		routine Meth( text : string ){ io.writeln( 'Meth(text)' ) }
	}
	return klass;
}

klass1 = CreateClass( 'AA', 123 );
klass2 = CreateClass( 'BB', 456 );

obj1 = klass1( 'abcdef' );
obj2 = klass2( 123456 );

obj1.Meth();
obj2.Meth();
obj1.Meth( 'abc' );
obj2.Meth( 'def' );
obj1.Extra();

io.writeln( obj1.added ); 
obj1.added = 987; # error modify const
io.writeln( obj1.added ); 
